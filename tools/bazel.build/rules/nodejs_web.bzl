load("//tools/bazel.build:binary_wrapper.bzl", "sh_binary_wrapper_impl")
load("//tools/bazel.build/rules:nodejs.bzl", "BzdNodeJsDepsProvider", "BzdNodeJsInstallProvider", "bzd_nodejs_install")
load("//tools/bazel.build/rules:package.bzl", "BzdPackageFragment")

BzdNodeJsWebProvider = provider(fields = ["tar"])

"""
Build and package a web application with webpack
"""

def _bzd_nodejs_web_build_impl(ctx):
    # Retrieve install artefacts generated by ctx.attr.install
    node_modules = ctx.attr.install[BzdNodeJsInstallProvider].node_modules
    package_json = ctx.attr.install[BzdNodeJsInstallProvider].package_json
    aliases = ctx.attr.install[BzdNodeJsInstallProvider].aliases

    # Retrieve source files
    srcs = ctx.attr.install[BzdNodeJsDepsProvider].srcs.to_list()

    # --- Generate the webpack.config.json file
    webpack_config = ctx.actions.declare_file("webpack.config.js")

    # aliases
    aliases = ["\"[{}]\": Path.join(Path.resolve(__dirname), \"{}\")".format(name, path) for name, path in aliases.items()]

    # entries and templates
    entries = ""
    templates = ""
    for entry_point, name in ctx.attr.entries.items():
        entry_point_files = entry_point.files.to_list()
        if len(entry_point_files) != 1:
            fail("Only a single file should be associated with entry '{}'.".format(name))
        entries += "\"{}\": Path.join(Path.resolve(__dirname), \"{}\"),\n".format(name, entry_point_files[0].short_path)

        # For HTML templates
        templates += "\"{}\": {{ entryId: \"{}\" }},".format(name, name)

    # Identify the build mode
    mode = None
    if ctx.attr.build_type == "prod":
        mode = "production"
    elif ctx.attr.build_type == "dev":
        mode = "development"
    else:
        fail("Invalid build type, only values 'prod' or 'dev' are supported.")

    # Generate the template file
    ctx.actions.expand_template(
        template = ctx.file._webpack_config_template,
        output = webpack_config,
        substitutions = {
            "{mode}": mode,
            "{public_path}": ctx.attr.public_path,
            "{aliases}": ",\n".join(aliases),
            "{entries}": entries,
            "{templates}": templates,
        },
    )

    # Gather toolchain manager
    toolchain_executable = ctx.toolchains["//tools/bazel.build/toolchains/nodejs:toolchain_type"].executable

    # Create a wrapped binary to have a self contained execution environment
    return sh_binary_wrapper_impl(
        ctx = ctx,
        binary = toolchain_executable.manager,
        output = ctx.outputs.executable,
        command = """
        {binary} --cwd "{workspace}" run webpack --output-path ".bzd/output"
        tar -h -cf "$1" -C "{workspace}/.bzd/output" --transform 's,^./,,' .
        """,
        extra_runfiles = [webpack_config, package_json, node_modules] + srcs,
        symlinks = {
            "node_modules": node_modules,
            "package.json": package_json,
            "webpack.config.cjs": webpack_config,
        },
    )

_bzd_nodejs_web_build = rule(
    implementation = _bzd_nodejs_web_build_impl,
    attrs = {
        "target_name": attr.string(
            mandatory = True,
            doc = "Name of actual target.",
        ),
        "alias": attr.string(
            doc = "Name of the alias, available in the form [name], for the current directory.",
        ),
        "entries": attr.label_keyed_string_dict(
            allow_files = True,
            doc = "Associate entry names with source entry point.",
        ),
        "public_path": attr.string(
            default = "/",
            doc = "Defines from which path the user will access these entries.",
        ),
        "install": attr.label(
            mandatory = True,
        ),
        "build_type": attr.string(
            mandatory = True,
            doc = "Build type eth for production or development.",
        ),
        "_webpack_config_template": attr.label(
            default = Label("//tools/bazel.build/rules/assets/nodejs:webpack_config_template"),
            allow_single_file = True,
        ),
    },
    executable = True,
    toolchains = ["//tools/bazel.build/toolchains/nodejs:toolchain_type"],
)

"""
NodeJs web application packaging
"""

def _bzd_nodejs_web_package_impl(ctx):
    # Build the application
    package = ctx.actions.declare_file(".bzd/nodejs_web_{}.tar".format(ctx.label.name))
    ctx.actions.run(
        inputs = [],
        outputs = [package],
        arguments = [package.path],
        progress_message = "Building {}".format(ctx.label),
        executable = ctx.executable.build,
    )
    return [
        DefaultInfo(files = depset([package])),
        BzdNodeJsWebProvider(tar = package),
    ]

_bzd_nodejs_web_package = rule(
    implementation = _bzd_nodejs_web_package_impl,
    attrs = {
        "target_name": attr.string(
            mandatory = True,
            doc = "Name of actual target.",
        ),
        "build": attr.label(
            executable = True,
            cfg = "host",
        ),
    },
)

"""
NodeJs web application executor
"""

def _bzd_nodejs_web_exec_impl(ctx):
    package = ctx.attr.package[BzdNodeJsWebProvider].tar

    # Run the application
    return [
        sh_binary_wrapper_impl(
            ctx = ctx,
            binary = ctx.attr._web_server,
            output = ctx.outputs.executable,
            command = "{{binary}} $@ \"{}\"".format(package.short_path),
            extra_runfiles = [package],
        ),
        BzdPackageFragment(
            tars = [
                package,
            ],
        ),
    ]

_bzd_nodejs_web_exec = rule(
    implementation = _bzd_nodejs_web_exec_impl,
    attrs = {
        "package": attr.label(
        ),
        "_web_server": attr.label(
            executable = True,
            cfg = "target",
            allow_files = True,
            default = Label("//tools/scripts:web_server"),
        ),
    },
    executable = True,
)

"""
Public macro to create a web application with NodeJs
"""

def bzd_nodejs_web(name, alias = "", srcs = [], packages = {}, deps = [], visibility = [], **kwargs):
    # Gather dependencies and install the packages
    bzd_nodejs_install(
        name = name + ".install",
        alias = alias,
        srcs = srcs,
        packages = packages,
        deps = deps + [
            "//nodejs:webpack",
        ],
        tags = ["nodejs"],
    )

    # Build the web application and packs it
    _bzd_nodejs_web_build(
        name = name + ".build",
        target_name = name,
        install = name + ".install",
        tags = ["nodejs"],
        build_type = select({
            "//tools/bazel.build/config:prod": "prod",
            "//tools/bazel.build/config:dev": "dev",
        }),
        **kwargs
    )

    # Generate the package
    _bzd_nodejs_web_package(
        name = name + ".package",
        build = name + ".build",
        target_name = name,
        tags = ["nodejs"],
        visibility = visibility,
    )

    # Generate the executable
    _bzd_nodejs_web_exec(
        name = name,
        package = name + ".package",
        tags = ["nodejs"],
        visibility = visibility,
    )
